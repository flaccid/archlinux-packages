# Maintainer: Chris Fordham <chris at fordham-nagy dot id dot au> aka flaccid
# Package Source: https://github.com/flaccid/archlinux-packages/blob/master/aws-cli-git/PKGBUILD

pkgname=aws-cli-git
_gitname=aws-cli
pkgver=20131102
pkgrel=1
pkgdesc="Universal Command Line Interface for Amazon Web Services."
arch=('any')
url="https://github.com/aws/aws-cli/"
license=('Apache 2')
depends=(
  'python'
  'python-botocore'
  'python-colorama'
  'python-bcdoc'
  'python-rsa'
  'python-jmespath'
  'python-ply'
)
makedepends=('python-distribute')
conflicts=('python2-aws-cli' 'python-awscli' 'awscli' 'aws-cli')
replaces=('python-aws-cli' 'awscli' 'aws-cli')
options=(!emptydirs)
source=("git://github.com/aws/${_gitname}.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

package() {
  cd "$srcdir/$_gitname"
  python setup.py install --root="$pkgdir/" --optimize=1

  msg 'Install Zsh completion script.'
	install -D -m644 "$srcdir/$_gitname/bin/aws_zsh_completer.sh" "$pkgdir/etc/zsh/aws_complete.zsh"

  msg 'Install requirements.txt.'
	install -D -m644 "$srcdir/$_gitname/requirements.txt" "$pkgdir/usr/share/doc/$_gitname/requirements.txt"

  msg 'Install LICENSE.'
	install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$_gitname/LICENSE"
}

# vim:set ts=2 sw=2 et:
