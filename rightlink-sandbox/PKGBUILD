# $Id$
# Maintainer: Chris Fordham <chris at fordham-nagy dot id dot au> aka flaccid
# Contributor: Brian Szmyd <brian.szmyd@rightscale.com>
# Package Source: https://github.com/flaccid/archlinux-packages/blob/master/rightlink-sandbox/PKGBUILD

pkgname=rightlink-sandbox
pkgver=5.9.5
pkgrel=3
pkgdesc='RightScale RightLink Ruby/RubyGems sandbox.'
arch=('i686' 'x86_64' 'armv6h')
url="https://github.com/rightscale/right_link/"
license=(MIT RUBY)
makedepends=(autoconf sed tar libxslt ruby1.8 rubygems1.8)
conflicts=(rightscale-deb)
options=(emptydirs strip !docs !libtool !staticlibs )

_rl_prefix="/opt/rightscale"
_sandbox_dir="${_rl_prefix}/sandbox"
_rb_gitname=ruby
_rb_pkgver=1.8.7-p371
_rb_gitdir="${_rb_gitname}-${_rb_pkgver}"
_rg_gitname=rubygems
_rg_pkgver=1.6.2
_rg_gitdir="${_rg_gitname}-${_rg_pkgver}"
_rg_install_opts='--no-rdoc --no-ri'

source=(
	"http://ftp.ruby-lang.org/pub/ruby/1.8/${_rb_gitname}-${_rb_pkgver}.tar.gz"
	"http://production.cf.rubygems.org/rubygems/${_rg_gitname}-${_rg_pkgver}.tgz"
	"https://raw.github.com/rightscale/right_link/v${pkgver}/Gemfile"
	"https://raw.github.com/rightscale/right_link/v${pkgver}/Gemfile.lock"
)
md5sums=('653f07bb45f03d0bf3910491288764df'
         '0c95a9869914ba1a45bf71d3b8048420'
         'fb225f42a80610de2b156ec683757ca9'
         '72a42050d24221c82939a4b8e8f5493e')

ruby_cmd() {
  # These are some LOAD_PATHs that need to be manually added to ruby
  # since it's not installed in the /opt yet.
  local lib_paths=$(for path in ${ruby_libs[@]}; do echo "-I${ruby_dir}/lib/ruby/${path}"; done)

  RUBYOPT="${lib_paths}" \
    ${ruby_dir}/bin/ruby ${@}
}

build() {
  msg 'Building Ruby...'
  pushd ${_rb_gitdir}
    autoconf
    ./configure \
      --prefix="${_sandbox_dir}" \
      --enable-pthread
    make
  popd
}

package() {
  ruby_dir=${pkgdir}/${_sandbox_dir}
  ruby_libs=()

  msg 'Installing Ruby into package...'
  pushd ${_rb_gitdir}
    make DESTDIR=${pkgdir} install
    ruby_libs+=(1.8 1.8/${CARCH}-linux)
  popd

  msg 'Installing RubyGems into package...'
  pushd ${_rg_gitdir}
    msg 'Running setup.rb...'
    ruby_cmd ./setup.rb --no-ri --no-rdoc

    ruby_libs+=(site_ruby/1.8)

    msg "Modifying shebang in ${ruby_dir}/bin/gem..."
    sed -i "s@${pkgdir}@@" ${ruby_dir}/bin/gem
  popd

  msg 'Installing bundler RubyGem...'
  gem-1.8 install bundler --user-install $_rg_install_opts
    
  msg 'Copying bundle(r) RubyGems from system user to sandbox in pkg...'
  mkdir -p ${pkgdir}/${_sandbox_dir}/lib/ruby/gems/1.8/gems
  mkdir -p ${pkgdir}/${_sandbox_dir}/lib/ruby/gems/1.8/bin
  cp -R "$HOME"/.gem/ruby/1.8/gems/bundle* ${pkgdir}/${_sandbox_dir}/lib/ruby/gems/1.8/gems/
  cp -R "$HOME"/.gem/ruby/1.8/bin/bundle ${pkgdir}/${_sandbox_dir}/lib/ruby/gems/1.8/bin/

  pushd ${srcdir}
    msg "Displaying package's gem environment."
    ruby_cmd ${ruby_dir}/bin/gem env
    msg 'Performing bundle package...'
    sed -i 's/gemspec/#gemspec/' "$srcdir/Gemfile"      # not sure why this is needed
    ruby_cmd ${ruby_dir}/lib/ruby/gems/1.8/bin/bundle package
  popd
  
  # TODO: find out why; fix in da upstream
  msg 'Adding in missing gem versions...'
  ruby_cmd ${ruby_dir}/bin/gem install right_link --version="$pkgver" $_rg_install_opts
  ruby_cmd ${ruby_dir}/bin/gem install trollop --version=2.0 $_rg_install_opts
  ruby_cmd ${ruby_dir}/bin/gem install encryptor --version=1.1.3 $_rg_install_opts
}

# vim:syntax=sh