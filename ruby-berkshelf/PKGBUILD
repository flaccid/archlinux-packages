# $Id$
# Maintainer: Chris Fordham <chris at fordham-nagy dot id dot au> aka flaccid
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>
# Package Build Source: https://github.com/flaccid/archlinux-packages/blob/master/ruby-berkshelf/PKGBUILD

_gemname=berkshelf
pkgname=ruby-$_gemname
pkgver=3.2.2
pkgrel=1
pkgdesc="Manages a Cookbook's, or an Application's Cookbook dependencies"
arch=(any)
url='http://berkshelf.com'
license=('Apache 2.0')
depends=(ruby ruby-addressable ruby-berkshelf-api-client ruby-buff-config ruby-buff-extensions ruby-buff-shell_out ruby-cleanroom ruby-faraday ruby-minitar ruby-retryable ruby-ridley ruby-solve ruby-thor ruby-octokit ruby-celluloid ruby-celluloid-io)
options=(!emptydirs)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
sha1sums=('e5bdbc56e59c90a45261a1ff9a01fdbe74ea854c')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
