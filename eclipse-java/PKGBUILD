# $Id$
# Maintainer: Chris Fordham <chris [at] fordham [hyphon] nagy [dot] id [dot] au> aka flaccid
# Contributor: Vitaliy Berdinskikh <ur6lad@archlinux.org.ua> aka UR6LAD
# Contributor: Jack Zhang <moonboots08@gmail.com>
# Package Build Source: https://github.com/flaccid/archlinux-packages/blob/master/eclipse-java/PKGBUILD

_eclipse_name=luna
_eclipse_release=SR1a
_eclipse_timestamp=201202080800

# releases:
# http://ftp.osuosl.org/pub/eclipse/technology/epp/downloads/release/release.xml

# mirror path:
# /technology/epp/downloads/release/luna/SR1/eclipse-java-luna-SR1a-linux-gtk-x86_64.tar.gz

pkgname=eclipse-java
pkgver="4.4.1"
pkgrel=3
pkgdesc="Eclipse IDE for Java Developers (present release)."
arch=('i686' 'x86_64')
url="http://www.eclipse.org/"
license=("EPL")
depends=('java-environment>=6' 'gtk2>=2.16.1' 'xulrunner>=1.9.0.10')
provides=('eclipse-java')
conflicts=('eclipse' 'eclipse-java-future')
replaces=('eclipse' 'eclipse-java-future')
changelog=${pkgname}.ChangeLog.markdown
install=${pkgname}.install

# defaults to x86_64 as AUR site does not eval $CARCH
source=(
  http://ftp.osuosl.org/pub/eclipse/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/eclipse-java-${_eclipse_name}-${_eclipse_release}-linux-gtk-x86_64.tar.gz
  eclipse-java.sh
  eclipse-java.desktop
  eclipse-java.svg
)

sha256sums=('2d9f32532b1eefb9c50de131daf6a4f8c29151f7e795e2086d7f277362ebde98'
            'f8a17d7abe283417d2b812647e5088c631a12ed90527bc462c3822f436b5e129'
            '06c56b9d54df9f93bd9fded8c846eb45ab205347d490dcdf76bb8063f3ba93f6'
            '6fe3ab198af244f9c8c2463b6837855506e811f61e5fd8ac7c9d5fe348830a14')

# override source for 32-bit
[ "$CARCH" = "i686" ] && source[0]="http://ftp.osuosl.org/pub/eclipse/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/eclipse-java-${_eclipse_name}-${_eclipse_release}-linux-gtk.tar.gz"
[ "$CARCH" = "i686" ] && sha256sums[0]='e4853892f52a5f19f4affa2f62eec422ccc3ec30a17f7bd3dbbe9d3d2b371dc1'

package() {
  local _icon_path=/usr/share/eclipse-java/plugins/org.eclipse.platform_${pkgver}.v${_eclipse_timestamp}

  install -m755 -d $pkgdir/usr/{bin,share/applications}
  install -m755 -d $pkgdir/usr/share/icons/hicolor/{16x16,32x32,48x48,scalable}/apps

  cd "$srcdir"
  mv eclipse "$pkgdir/usr/share/eclipse-java"

  install -D -m755 eclipse-java.sh $pkgdir/usr/bin/eclipse-java
  install -D -m644 eclipse-java.desktop $pkgdir/usr/share/applications
  install -D -m644 eclipse-java.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/eclipse-java.svg

  ln -s ${_icon_path}/eclipse.png ${pkgdir}/usr/share/icons/hicolor/16x16/apps/eclipse-java.png
  ln -s ${_icon_path}/eclipse32.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/eclipse-java.png
  ln -s ${_icon_path}/eclipse48.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/eclipse-java.png
}

# vim:set ts= 2 sw=2 Et:
