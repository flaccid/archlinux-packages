# $Id$
# Maintainer: Chris Fordham <chris at fordham-nagy dot id dot au> aka flaccid
# Contributor: Alper KANAT <alperkanat@raptiye.org>
# Package Source: https://github.com/flaccid/archlinux-packages/blob/master/aws-cli/PKGBUILD

pkgname=aws-cli
pkgver=1.7.25
pkgrel=1
pkgdesc="Universal Command Line Interface for Amazon Web Services."
arch=('any')
url="https://github.com/aws/aws-cli"
license=('Apache 2')
depends=(
  'python'
  'python-bcdoc<0.15.0'
  'python-botocore<0.106.0'
  'python-colorama<0.4.0'
  'python-docutils>=0.10'
  'python-rsa<3.2.0'
)
makedepends=('python-setuptools')
conflicts=('python2-aws-cli' 'python-awscli' 'awscli')
replaces=('python-aws-cli' 'awscli')
options=(!emptydirs)
source=("https://github.com/aws/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('ff7b93860a90783c644dbf0b39fee88ee30cc74f631be89c953facb49f081522')

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1

  msg 'Install Zsh completion script.'
  install -D -m644 "$srcdir/$pkgname-$pkgver/bin/aws_zsh_completer.sh" "$pkgdir/etc/zsh/aws_complete.zsh"

  msg 'Install requirements.txt.'
  install -D -m644 "$srcdir/$pkgname-$pkgver/requirements.txt" "$pkgdir/usr/share/doc/$pkgname/requirements.txt"

  msg 'Install LICENSE.'
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
