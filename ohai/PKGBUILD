# $Id$
# Maintainer: Chris Fordham <chris at fordham-nagy dot id dot au> aka flaccid
# Package Source: https://github.com/flaccid/archlinux-packages/blob/master/ohai/PKGBUILD

_gemname=ohai
pkgname="$_gemname"
pkgver=6.20.0
pkgrel=2
pkgdesc="Ohai profiles your system and emits JSON."
arch=(any)
url="http://docs.opscode.com/ohai.html"
license=('Apache 2.0')
depends=(
  'ruby'
  'ruby-ipaddress'
  'ruby-mixlib-cli'
  'ruby-mixlib-config'
  'ruby-mixlib-log'
  'ruby-mixlib-shellout'
  'ruby-systemu>=2.5.2'
  'ruby-yajl-ruby'
)
makedepends=(rubygems)
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
md5sums=('e3a76bc8e8433e1afe1bb30548603976')

package() {
  cd "$srcdir"

  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
}

# vim:set ts=2 sw=2 et:
