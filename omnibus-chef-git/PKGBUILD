# $Id$
# Maintainer: Chris Fordham <chris [at] fordham-nagy [dot] id [dot] au> aka flaccid
# Package Build Source: https://github.com/flaccid/archlinux-packages/blob/master/omnibus-chef-git/PKGBUILD

# Build a specific version by environment variable instead of editing this file:
# CHEF_VERSION=11.10.4 makepkg -s

# Build with different log level
# OMNIBUS_LOG_LEVEL=debug makepkg -s

# Skip build, assume already built previously
# OMNIBUS_SKIP_BUILD=1 makepkg -sf

# omnibus-chef (FPM) uses directories under system locations such as /opt and /var,
#     making user builds a pain for non-root users!

: "${CHEF_VERSION:=12.6.0}"
: "${OMNIBUS_LOG_LEVEL:=warn}"
: "${OMNIBUS_SKIP_BUILD:=0}"

pkgname=omnibus-chef-git
pkgver=12.5.0.current.0.r2733.g852e914
pkgrel=1
_gitref=master
pkgdesc="Omnibus packaging for Opscode Chef (provides a Makeself installer for Chef $CHEF_VERSION)."
arch=(any)
url="https://github.com/chef/chef"
license=('Apache 2.0')
install="$pkgname.install"
makedepends=(git ruby-bundler)
conflicts=(omnibus-chef)
replaces=(omnibus-chef)
source=("chef::git+https://github.com/chef/chef.git#branch=$_gitref")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/chef" && git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  msg 'Initialising system chef directories.'
  # need root :(
  [ ! -e /opt/chef ] && sudo mkdir -p /opt/chef
  [ ! -e /var/cache/omnibus ] && sudo mkdir -p /var/cache/omnibus
  sudo chown "$(whoami)" /opt/chef /var/cache/omnibus
  sudo rm -Rf /opt/chef/* /var/cache/omnibus/*

  msg 'Ensuring system chef directories are clean.'
  rm -Rf /opt/chef/* /var/cache/omnibus/*

  pushd "$srcdir/chef/omnibus"
    msg 'Preparing bundle...'
    bundle install --binstubs
    # bundle update omnibus-software omnibus
    bundle exec omnibus clean chef
  popd

  build_version="$(grep build_version $srcdir/chef/omnibus/config/projects/chef.rb | awk -F '"' '{print $2}')"
  echo "Build version in chef.rb is $build_version."
  echo "override :chef,   version: \"$CHEF_VERSION\"" \
    >> "$srcdir/chef/omnibus/config/projects/chef.rb"
}

build() {
  if [ ! "$OMNIBUS_SKIP_BUILD" -eq 1 ]; then
    pushd "$srcdir/chef/omnibus"
      msg "Building omnibus chef (version $CHEF_VERSION)..."
      # bundle exec bundle list
      bundle exec omnibus build chef -l "$OMNIBUS_LOG_LEVEL"
    popd
  else
    msg 'Skipping omnibus chef build.'
  fi

  # finally, ensure ownership of root
  sudo chown -R root /opt/chef /var/cache/omnibus
}

package() {
  install -m755 -d "$pkgdir/usr/local/sbin"
  cp -v "$srcdir/chef/omnibus/pkg/chef"-* "$pkgdir/usr/local/sbin/"
  mkdir -p "$pkgdir/usr/local/share/omnibus-chef"
  mv -v "$pkgdir/usr/local/sbin/"*.metadata.json "$pkgdir/usr/local/share/omnibus-chef/"
  pushd "$pkgdir/usr/local/sbin"
    ln -sv ./chef-${CHEF_VERSION}_0.arch.$(uname -r).sh ./chef-installer
  popd
}

# vim:set ts= 2 sw=2 Et:
