# Maintainer: Chris Fordham <chris at fordham-nagy dot id dot au> aka flaccid
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>
# Package Source: https://github.com/flaccid/archlinux-packages/blob/master/chef/PKGBUILD

_gemname=chef
pkgname=chef
pkgver=11.8.0
pkgrel=1
pkgdesc="A systems integration framework, built to bring the benefits of configuration management to your entire infrastructure."
arch=(any)
url="http://www.opscode.com/chef/"
license=('Apache 2.0')
depends=('ruby'
  'chef-zero'
  'ruby-diff-lcs'
  'ruby-erubis'
  'ruby-highline'
  'ruby-json'
  'ruby-mime-types'
  'ruby-mixlib-authentication'
  'ruby-mixlib-cli'  
  'ruby-mixlib-config'
  'ruby-mixlib-log'
  'ruby-mixlib-shellout'
  'ruby-net-ssh'
  'ruby-net-ssh-multi'
  'ohai'
  'pry'
  'puma'
  'ruby-rest-client'
  'ruby-yajl-ruby'
)
makedepends=(rubygems)
conflicts=(ruby-chef)
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
md5sums=('522cb7dfcff513a575f6c68493030584')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem"
}

# vim:set ts= 2 sw=2 Et: