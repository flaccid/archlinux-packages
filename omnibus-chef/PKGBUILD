# $Id$
# Maintainer: Chris Fordham <chris at fordham-nagy dot id dot au> aka flaccid
# Package Source: https://github.com/flaccid/archlinux-packages/blob/master/omnibus-chef/PKGBUILD

# omnibus-chef (FPM) uses directories under system locations such as /opt and /var,
# making user builds impossible :(
# You will need to build the package with something like: sudo makepkg -s --asroot

# Build a specific version by environment variable instead of editing this file:
# sudo CHEF_VERSION=11.10.4 makepkg -s --asroot

pkgname=omnibus-chef
pkgver=11.6.2
_chefver=11.10.4.ohai7.0
pkgrel=7
pkgdesc="Omnibus packaging for Opscode Chef (provides a Makeself installer for Chef $_chefver)."
arch=(any)
url="https://github.com/opscode/omnibus-chef"
license=('Apache 2.0')
install="$pkgname.install"
makedepends=(git ruby-bundler)
# we're actually going to use master until a -git package is done
source=("$pkgname"::'git://github.com/opscode/omnibus-chef.git')
md5sums=('SKIP')

: "${CHEF_VERSION:=$_chefver}"

pkgver() {
  echo "$pkgver"
}

if [ ! "$USER" = 'root' ]; then
  echo 'Building omnibus chef requires root. Please run makepkg with --asroot under sudo.'
  exit 1
fi

prepare() {
  msg 'Creating system chef directories.'
  mkdir -p /opt/chef /var/cache/omnibus
  
  msg 'Ensuring system chef directories are clean.'
  rm -Rf /opt/chef/* /var/cache/omnibus/*

  pushd "$srcdir/$pkgname"
    msg 'Preparing bundle...'
    bundle update omnibus-software
    bundle exec omnibus clean chef
  popd
  
  # why is this needed?! (use the system's bundler)
  mkdir -p /opt/chef/embedded/bin
  ln -svf /usr/bin/bundle /opt/chef/embedded/bin/bundle
}

build() {
  cd "$srcdir/$pkgname"

  echo "override :chef,   version: \"$CHEF_VERSION\"" >> config/projects/chef.rb

  msg "Building omnibus chef (version $CHEF_VERSION)..."
  bundle exec omnibus build project chef
}

package() {
  install -m755 -d "$pkgdir/usr/local/sbin"
  cp -v "$srcdir/$pkgname"/pkg/chef-* "$pkgdir/usr/local/sbin/"
  rm -f "$srcdir/$pkgname"/pkg/chef-*.metadata.json
  cd "$pkgdir/usr/local/sbin" && ln -sv ./chef-${CHEF_VERSION}_0.arch.$(uname -r).sh ./chef-installer
}

# vim:set ts= 2 sw=2 Et:
