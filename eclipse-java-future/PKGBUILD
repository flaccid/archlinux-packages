# $Id$
# Maintainer: Chris Fordham <chris [at] fordham [hyphon] nagy [dot] id [dot] au> aka flaccid
# Package Build Source: https://github.com/flaccid/archlinux-packages/blob/master/eclipse-java-future/PKGBUILD

_eclipse_name=mars
_eclipse_release=M1
_eclipse_timestamp=201202080800

# mirror path:
# /technology/epp/downloads/release/mars/M1/eclipse-java-mars-M1-linux-gtk-x86_64.tar.gz

pkgname=eclipse-java-future
_pkgname=eclipse-java
pkgver=4.5.0
pkgrel=1
pkgdesc="Eclipse IDE for Java Developers (future release)."
arch=('i686' 'x86_64')
url="http://www.eclipse.org"
license=("EPL")
depends=('java-environment>=6' 'gtk3' 'xulrunner>=1.9.0.10')
makedepends=('unzip' 'patch')
provides=('eclipse')
conflicts=('eclipse' 'eclipse-java')
replaces=('eclipse' 'eclipse-java')
changelog=${pkgname}.ChangeLog.markdown
install=${_pkgname}.install

source=(
  http://ftp.osuosl.org/pub/eclipse/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/eclipse-java-${_eclipse_name}-${_eclipse_release}-linux-gtk-x86_64.tar.gz
  eclipse.sh
  eclipse.desktop
  eclipse.svg
)

sha256sums=('098aedcd5651c2832e001d86f41929cc70566109b3f3a4bb3be30a2dc7437a53'
            '2f35fd3ba09c32c0f98421c6b77a1a31d72370d2fb69e99fbc0a438ba821f8bd'
            '4eb2189c96fcfa340886b049b34dc3636d7b2bfa865140dc72edb61455d900c3'
            '6fe3ab198af244f9c8c2463b6837855506e811f61e5fd8ac7c9d5fe348830a14')

# override source for 32-bit
[ "$CARCH" = "i686" ] && source[0]="http://ftp.osuosl.org/pub/eclipse/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/eclipse-java-${_eclipse_name}-${_eclipse_release}-linux-gtk.tar.gz"
[ "$CARCH" = "i686" ] && sha256sums[0]='3bafb196784ded53f56ea5dba0a8114ab929766820e142ef417ac22d6730494e'

package() {
	local _icon_path=/usr/share/eclipse/plugins/org.eclipse.platform_${pkgver}.v${_eclipse_timestamp}

	install -m755 -d $pkgdir/usr/{bin,share/applications}
	install -m755 -d $pkgdir/usr/share/icons/hicolor/{16x16,32x32,48x48,scalable}/apps

	cd "$srcdir"
  mv eclipse "$pkgdir/usr/share"

	install -D -m755 eclipse.sh $pkgdir/usr/bin/eclipse
	install -D -m644 eclipse.desktop $pkgdir/usr/share/applications
	install -D -m644 eclipse.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/eclipse.svg

  ln -s ${_icon_path}/eclipse.png ${pkgdir}/usr/share/icons/hicolor/16x16/apps/eclipse.png
	ln -s ${_icon_path}/eclipse32.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/eclipse.png
	ln -s ${_icon_path}/eclipse48.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/eclipse.png
}

# vim:set ts= 2 sw=2 Et:
